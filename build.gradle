buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven { url "http://palantir.bintray.com/releases" }
    }

    dependencies {
        classpath 'gradle.plugin.com.palantir:gradle-circle-style:1.1.2'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.3'
        classpath 'com.netflix.nebula:nebula-dependency-recommender:6.0.0'
        classpath 'com.netflix.nebula:nebula-publishing-plugin:5.1.5'
        classpath 'com.palantir.baseline:gradle-baseline-java:0.17.1'
        classpath 'com.palantir.configurationresolver:gradle-configuration-resolver-plugin:0.3.0'
        classpath 'com.palantir.gradle.lockfiles:gradle-lock-files:0.3.0-8-gb435117.dirty'
    }
}

plugins {
    id 'com.palantir.git-version' version '0.11.0'
    id 'org.inferred.processors' version '1.2.15'
}

apply plugin: 'com.palantir.baseline-config' // provides baselineUpdateConfig task
apply plugin: 'com.palantir.baseline-idea'
apply plugin: 'com.palantir.circle.style'

apply plugin: 'com.palantir.lock-files'

repositories {
    maven { url  "http://palantir.bintray.com/releases" }
}

dependencies {
    baseline "com.palantir.baseline:gradle-baseline-java-config:0.17.1@zip"
}

allprojects {
    version gitVersion()
    group 'com.palantir.conjure.java.runtime'

    apply plugin: 'com.palantir.configuration-resolver'
    apply plugin: 'nebula.dependency-recommender'
    dependencyRecommendations {
        propertiesFile file: project.rootProject.file('versions.props')
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'com.palantir.baseline-checkstyle'
    apply plugin: 'com.palantir.baseline-eclipse'
    apply plugin: 'com.palantir.baseline-idea'
    apply plugin: 'com.palantir.baseline-error-prone'

    repositories {
        jcenter()
        maven { url "http://palantir.bintray.com/releases" }
    }

    sourceCompatibility = 1.8

    tasks.withType(Javadoc) {
        // suppress Javadoc doclint warnings in Java 8+
        if (!System.getProperty("java.version").startsWith("1.7")) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
    tasks.check.dependsOn(javadoc)

    test {
        minHeapSize = "512m"
        maxHeapSize = "512m"
    }

    configurations.errorprone {
        resolutionStrategy {
            force 'com.google.guava:guava:21.0'
        }
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs += ['-XepDisableWarningsInGeneratedCode']
    }

//    dependencies {
//        constraints {
//            implementation('com.google.guava:guava') {
//                version {
//                    strictly '15.0'
//                    because 'testing'
//                }
//            }
//        }
//    }
}
