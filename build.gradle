buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "com.palantir:gradle-baseline-java:${baselineVersion}"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:${bintrayPlugin}"
    }
}

plugins {
    id 'com.palantir.git-version' version '0.2.0'
    id 'org.inferred.processors' version '1.1.5'
}

apply plugin: 'com.palantir.baseline-config' // provides baselineUpdateConfig task
apply plugin: 'com.palantir.baseline-idea'

repositories {
    jcenter() // needed for baselineUpdateConfig task
}

allprojects {
    version gitVersion()
    group 'com.palantir.remoting'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'com.palantir.baseline-checkstyle'
    apply plugin: 'com.palantir.baseline-eclipse'
    apply plugin: 'com.palantir.baseline-findbugs'
    apply plugin: 'com.palantir.baseline-idea'
    apply plugin: 'org.inferred.processors'
    apply plugin: 'com.palantir.configuration-resolver'

    repositories {
        jcenter()
    }

    sourceCompatibility = 1.7

    task resolveDependencies {
        description = 'Forces the resolution of all dependencies in all configurations'
        group = 'build'

        doLast {
            configurations.all { c ->
                c.resolve()
            }
        }
    }

    if (System.env.CIRCLE_TEST_REPORTS) {
        test.reports.junitXml.destination = new File(System.env.CIRCLE_TEST_REPORTS, it.getName())
    }
}
