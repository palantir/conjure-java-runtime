buildscript {
    repositories {
        mavenCentral() { metadataSources { mavenPom(); ignoreGradleMetadataRedirection() } }
        gradlePluginPortal() { metadataSources { mavenPom(); ignoreGradleMetadataRedirection() } }
    }

    dependencies {
        classpath 'com.palantir.jakartapackagealignment:jakarta-package-alignment:0.6.0'
        classpath 'com.palantir.gradle.jdks:gradle-jdks:0.43.0'
        classpath 'com.palantir.gradle.jdkslatest:gradle-jdks-latest:0.14.0'
        classpath 'com.palantir.gradle.externalpublish:gradle-external-publish-plugin:1.15.0'
        classpath 'com.palantir.gradle.failure-reports:gradle-failure-reports:1.10.0'
        classpath 'com.palantir.javaformat:gradle-palantir-java-format:2.48.0'
        classpath 'com.palantir.gradle.revapi:gradle-revapi:1.7.0'
        classpath 'com.netflix.nebula:gradle-dependency-lock-plugin:7.0.1'
        classpath 'com.palantir.baseline:gradle-baseline-java:5.61.0'
        classpath 'com.palantir.gradle.gitversion:gradle-git-version:3.1.0'
        classpath 'com.palantir.metricschema:gradle-metric-schema:0.32.0'
        classpath 'gradle.plugin.org.inferred:gradle-processors:3.7.0'
        classpath 'com.palantir.gradle.consistentversions:gradle-consistent-versions:2.23.0'
    }
}

apply plugin: 'com.palantir.external-publish'
apply plugin: 'com.palantir.failure-reports'
apply plugin: 'com.palantir.baseline'
apply plugin: 'com.palantir.git-version'
apply plugin: 'com.palantir.consistent-versions'
apply plugin: 'com.palantir.baseline-java-versions'
apply plugin: 'com.palantir.jdks.latest'

javaVersions {
    libraryTarget = 11
    runtime = 21
}

version gitVersion()

allprojects {
    apply plugin: 'com.palantir.java-format'
    apply plugin: 'com.palantir.jakarta-package-alignment'
    version rootProject.version
    group 'com.palantir.conjure.java.runtime'

    repositories {
        mavenCentral() { metadataSources { mavenPom(); ignoreGradleMetadataRedirection() } }
    }

    configurations.all {
        resolutionStrategy {
            preferProjectModules()
        }

        resolutionStrategy.eachDependency { details ->
            if (details.requested.group == 'com.google.errorprone' && details.requested.name == 'error_prone_annotations') {
                details.useTarget group: 'com.google.errorprone', name: 'error_prone_annotations', version: '2.7.1'
                details.because "The error_prone_annotations dependency must be low to avoid forcing consumers to use newer releases"
            }
            if (details.requested.group == 'com.github.ben-manes.caffeine' && details.requested.name == 'caffeine' && details.requested.version == '3.1.7') {
                details.useTarget group: 'com.github.ben-manes.caffeine', name: 'caffeine', version: '3.1.6'
                details.because "https://github.com/ben-manes/caffeine/issues/1111"
            }
            if (details.requested.group.startsWith('com.fasterxml.jackson') && (details.requested.version == '2.16.0' || details.requested.version == '2.16.1')) {
                details.useTarget group: details.requested.group, name: details.requested.name, version: '2.15.3'
                details.because 'https://github.com/FasterXML/jackson-databind/pull/4230'
            }
        }
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'com.palantir.baseline-class-uniqueness'

    tasks.check.dependsOn javadoc, checkImplicitDependenciesMain, checkUnusedDependencies

    tasks.withType(JavaCompile) {
        options.compilerArgs += ['-Werror']
    }

    test {
        minHeapSize = "512m"
        maxHeapSize = "512m"
    }
}
