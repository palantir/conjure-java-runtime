buildscript {
    repositories {
        jcenter()
        maven { url "http://palantir.bintray.com/releases" }
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.3'
        classpath 'com.netflix.nebula:gradle-dependency-lock-plugin:4.9.5'
        classpath 'com.netflix.nebula:nebula-publishing-plugin:5.1.5'
        classpath 'com.palantir.baseline:gradle-baseline-java:0.35.0'
    }
}

plugins {
    id 'com.palantir.git-version' version '0.11.0'
    id 'org.inferred.processors' version '1.3.0'
}

apply plugin: 'com.palantir.baseline'

allprojects {
    version gitVersion()
    group 'com.palantir.conjure.java.runtime'

    repositories {
        jcenter()
        maven { url "http://palantir.bintray.com/releases" }
    }
}

subprojects {
    apply plugin: 'java'

    sourceCompatibility = 1.8

    apply plugin: 'nebula.dependency-lock'
    dependencyLock {
        includeTransitives = true
        lockFile = 'versions.lock'
        configurationNames = ['runtime', 'compileClasspath']
    }

    task verifyDependencyLocksAreCurrent {
        doLast {
            def expectedDependencies = tasks.saveLock.getOutputLock()
            def actualDependencies = tasks.saveLock.getGeneratedLock()
            def digester = java.security.MessageDigest.getInstance('SHA')
            logger.info("Verifying integrity of dependency locks: {} vs {}", expectedDependencies, actualDependencies)
            if (digester.digest(expectedDependencies.bytes) != digester.digest(actualDependencies.bytes)) {
                throw new GradleException("The dependencies of project " + project.name + " do not match the expected "
                    + "dependencies recorded in " + expectedDependencies + ". "
                    + "Run `./gradlew generateLock saveLock` and commit the updated version.lock files")
            }
        }
    }
    verifyDependencyLocksAreCurrent.dependsOn(generateLock)
    build.dependsOn(verifyDependencyLocksAreCurrent)

    tasks.withType(Javadoc) {
        // suppress Javadoc doclint warnings in Java 8+
        if (!System.getProperty("java.version").startsWith("1.7")) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
    tasks.check.dependsOn(javadoc)

    test {
        minHeapSize = "512m"
        maxHeapSize = "512m"
    }
}
