buildscript {
    repositories {
        gradlePluginPortal()
        maven { url "http://palantir.bintray.com/releases" }
    }

    dependencies {
        classpath 'com.palantir.gradle.revapi:gradle-revapi:1.0.5'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
        classpath 'com.netflix.nebula:gradle-dependency-lock-plugin:7.0.1'
        classpath 'com.netflix.nebula:nebula-publishing-plugin:14.0.0'
        classpath 'com.palantir.baseline:gradle-baseline-java:2.7.0'
        classpath 'com.palantir.gradle.gitversion:gradle-git-version:0.12.2'
        classpath 'gradle.plugin.org.inferred:gradle-processors:3.1.0'
        classpath 'com.palantir.gradle.consistentversions:gradle-consistent-versions:1.12.4'
    }
}

apply plugin: 'com.palantir.baseline'
apply plugin: 'com.palantir.git-version'
apply plugin: 'com.palantir.consistent-versions'

allprojects {
    version gitVersion()
    group 'com.palantir.conjure.java.runtime'

    repositories {
        jcenter()
        maven { url "http://palantir.bintray.com/releases" }
    }

    dependencies {
        constraints {
            rootConfiguration 'io.dropwizard.metrics:metrics-core', {
                version { strictly '[3, 4[' }
                because "Spark still uses 3.X, which can't co-exist with 4.X"
            }

            rootConfiguration 'com.squareup.retrofit2:retrofit', {
                version { strictly '2.4.0' }
                because 'Retrofit 2.5.0 breaks with our path parameter routing logic'
            }

            rootConfiguration 'com.squareup.okhttp3:okhttp', {
                version { strictly '[3, 3.14.0[' }
                because 'okhttp 3.14.0 removed UnrepeatableRequestBody which is relied on by an internal library'
            }
        }
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'com.palantir.baseline-class-uniqueness'

    sourceCompatibility = 1.8
    tasks.check.dependsOn(javadoc)

    test {
        minHeapSize = "512m"
        maxHeapSize = "512m"
    }
}
