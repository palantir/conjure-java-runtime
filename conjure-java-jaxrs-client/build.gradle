apply plugin: 'com.palantir.external-publish-jar'
apply plugin: 'com.palantir.revapi'
apply plugin: 'com.palantir.metric-schema'

versionsLock {
    disableJavaPluginDefaults()
}

dependencies {
    api project(":extras:refresh-utils")
    api project(":client-config")
    api project(":conjure-java-legacy-clients")
    api "com.google.code.findbugs:jsr305"
    // TODO(dsanduleac): Should be implementation, but can't because we expose feign.TextDelegateEncoder
    api "com.netflix.feign:feign-core", {
        // prefer jakarta.ws.rs:jakarta.ws.rs-api
        exclude group: 'javax.ws.rs', module: 'javax.ws.rs-api'
    }
    api "com.palantir.dialogue:dialogue-target"

    implementation project(":conjure-java-annotations")
    implementation "com.palantir.dialogue:dialogue-apache-hc5-client"
    implementation "com.palantir.dialogue:dialogue-core"
    implementation "com.palantir.dialogue:dialogue-serde"
    implementation 'com.palantir.safe-logging:logger'

    implementation 'com.palantir.tritium:tritium-registry'

    implementation project(":conjure-java-jackson-serialization")
    implementation "com.google.guava:guava"
    implementation "com.github.ben-manes.caffeine:caffeine"
    implementation "com.netflix.feign:feign-jackson"
    // this is unfortunately very deliberate. The client code _should_
    // work against both javax. and jakarta. annotations, however,
    // most of the server infrastructure needed to make this work cannot
    // possibly work with both of those at the same time, so this
    // very intentionally is bound to the jakarta version and we just
    // don't test the legacy javax version in this way
    testImplementation project(":conjure-java-jersey-jakarta-server")
    testImplementation project(':keystores')
    testImplementation "com.netflix.feign:feign-jackson"
    testImplementation "com.squareup.okhttp3:mockwebserver:4.10.0"
    testImplementation("io.dropwizard:dropwizard-testing")
    testImplementation "org.junit.jupiter:junit-jupiter"
    testImplementation "org.assertj:assertj-core"
    testImplementation "org.mockito:mockito-core"
    testImplementation "org.mockito:mockito-junit-jupiter"
    testImplementation "com.palantir.safe-logging:preconditions-assertj"

    constraints {
        testImplementation 'com.squareup.okhttp3:okhttp', {
            version { strictly '[4, 5[' }
            because 'this is only used in tests in this module'
        }
    }
}

configurations.all {
    // dropwizard dynamically searches for this if it's available, this breaks with
    // modern versions of Java 17+, so just exclude it since we don't want it on any
    // configuration
    exclude group: 'com.fasterxml.jackson.module', module: 'jackson-module-afterburner'
}

// feign.DefaultMethodHandler sets Lookup.IMPL_LOOKUP accessible resulting in
// InaccessibleObjectException: Unable to make field static final java.lang.invoke.MethodHandles$Lookup
// java.lang.invoke.MethodHandles$Lookup.IMPL_LOOKUP accessible: module java.base does not
// "opens java.lang.invoke" to unnamed module
moduleJvmArgs {
    opens 'java.base/java.lang.invoke'
}
