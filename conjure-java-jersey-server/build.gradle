apply plugin: 'com.palantir.external-publish-jar'
apply plugin: 'com.palantir.metric-schema'

def jerseyVersion = '2.34'

versionsLock {
    disableJavaPluginDefaults()
}

dependencies {
    api "com.palantir.conjure.java.api:errors"
    api "javax.ws.rs:javax.ws.rs-api"
    api "org.glassfish.jersey.core:jersey-server", {
        version {
            strictly jerseyVersion
        }
    }

    implementation 'javax.annotation:javax.annotation-api'
    implementation 'javax.inject:javax.inject'
    implementation 'com.palantir.tritium:tritium-registry'
    implementation 'io.dropwizard.metrics:metrics-core'
    implementation "com.fasterxml.jackson.jaxrs:jackson-jaxrs-cbor-provider", {
        // activation-api should be a compileOnly dependency where javax.activation should be used at runtime
        exclude module: 'javax.activation-api'
    }
    implementation "com.netflix.feign:feign-core"
    implementation "com.palantir.safe-logging:safe-logging"
    implementation "com.palantir.safe-logging:logger"
    implementation 'com.palantir.tokens:auth-tokens'
    implementation "com.palantir.tracing:tracing-jersey"
    implementation project(':conjure-java-jackson-serialization')

    runtimeOnly "org.glassfish.jersey.ext:jersey-bean-validation", {
        version {
            strictly jerseyVersion
        }
    }
    runtimeOnly 'org.glassfish.jersey.inject:jersey-hk2', {
        version {
            strictly jerseyVersion
        }
    }
}

configurations.all {
    resolutionStrategy {
        // Conflicts with javax.inject:javax.inject:1
        // This is safe to remove, the purpose of the repackaged version is to implement OSGI, which we don't use
        // Source: https://stackoverflow.com/a/25218230/274699
        exclude group: 'org.glassfish.hk2.external', module: 'jakarta.inject'
    }
}
