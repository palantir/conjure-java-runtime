apply plugin: 'com.palantir.external-publish-jar'
apply plugin: 'com.palantir.metric-schema'

versionsLock {
    disableJavaPluginDefaults()
}

dependencies {
    api "com.palantir.conjure.java.api:errors"
    api "org.glassfish.jersey.core:jersey-server:2.26"
    implementation "org.glassfish.jersey.core:jersey-common:2.26"

    implementation 'com.palantir.tritium:tritium-registry'
    implementation 'io.dropwizard.metrics:metrics-core'
    implementation 'org.glassfish.jersey.inject:jersey-hk2:2.26'
    implementation "com.fasterxml.jackson.jaxrs:jackson-jaxrs-cbor-provider"
    implementation "com.jcraft:jzlib"
    implementation "com.netflix.feign:feign-core"
    implementation "com.palantir.safe-logging:safe-logging"
    implementation 'com.palantir.tokens:auth-tokens'
    implementation "com.palantir.tracing:tracing-jersey"
    implementation project(':conjure-java-jackson-serialization')

    runtimeOnly "org.glassfish.jersey.ext:jersey-bean-validation"

    testImplementation project(':conjure-java-jackson-serialization')
    testImplementation 'org.glassfish.jersey.media:jersey-media-json-jackson'
    testImplementation "com.fasterxml.jackson.core:jackson-databind"
    testImplementation "com.palantir.tracing:tracing"
    testImplementation "com.palantir.conjure.java.api:ssl-config"
    testImplementation("io.dropwizard:dropwizard-testing:2.0.10")
    testImplementation "junit:junit"
    testImplementation "org.assertj:assertj-core"
    testImplementation "org.hamcrest:hamcrest-all"
    testImplementation "org.mockito:mockito-core"
    implementation 'com.palantir.safe-logging:logger'

    modules {
        module("org.glassfish.hk2.external:javax.inject") {
            // Conflicts with javax.inject:javax.inject:1
            // This is safe to remove, the purpose of the repackaged version is to implement OSGI, which we don't use
            // Source: https://stackoverflow.com/a/25218230/274699
            replacedBy("javax.inject:javax.inject", "javax version is preferred")
        }
        module("com.sun.activation:jakarta.activation") {
            replacedBy('jakarta.activation:jakarta.activation-api', "jakarta version is preferred in this case")
        }
    }

    constraints {
        rootConfiguration "io.dropwizard:dropwizard-testing", {
            version { strictly "2.0.10" }
            because "2.x is compatible with jaxrs"
        }
        rootConfiguration "org.glassfish.jersey.inject:jersey-hk2", {
            version { strictly "2.26" }
            because "2.x is compatible with jaxrs"
        }
        rootConfiguration "org.glassfish.jersey.core:jersey-common", {
            version { strictly "2.26" }
            because "2.x is compatible with jaxrs"
        }
        rootConfiguration "org.glassfish.jersey.core:jersey-server", {
            version { strictly "2.26" }
            because "2.x is compatible with jaxrs"
        }
    }
}
