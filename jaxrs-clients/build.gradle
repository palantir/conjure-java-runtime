apply plugin: 'com.github.johnrengelman.shadow'

dependencies {
    compile project(":error-handling")
    compile project(":ext:brave-extensions")
    compile project(":http-clients-api")
    compile project(":service-config")

    compile "com.fasterxml.jackson.core:jackson-databind"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-guava"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jdk7"
    compile "com.google.code.findbugs:jsr305"
    compile "com.google.guava:guava"
    compile "com.netflix.feign:feign-jackson"
    compile("com.netflix.feign:feign-jaxrs") {
        // the shipped version clashes with the newer javax.ws.rs:javax.ws.rs-api used by (e.g.) dropwizard
        exclude group: "javax.ws.rs", module: "jsr311-api"
    }
    compile "com.netflix.feign:feign-okhttp"
    compile "io.zipkin.brave:brave-okhttp"
    compile "javax.ws.rs:javax.ws.rs-api"
    compile "org.slf4j:slf4j-api"
    compile "com.netflix.feign:feign-slf4j"

    testCompile project(":http-servers")
    testCompile "com.squareup.okhttp3:mockwebserver"
    testCompile "io.dropwizard:dropwizard-testing"
    testCompile "junit:junit"
    testCompile "org.hamcrest:hamcrest-all"

    // Keep in sync with the dependencies excluded from shadowJar.
    shadow project(':error-handling')
    shadow project(':service-config')
    shadow project(':ssl-config')
    shadow "javax.ws.rs:javax.ws.rs-api"
    shadow "com.fasterxml.jackson.core:jackson-databind"
    shadow "com.fasterxml.jackson.datatype:jackson-datatype-guava"
    shadow "com.fasterxml.jackson.datatype:jackson-datatype-jdk7"
}

shadowJar {
    mergeServiceFiles()
    relocate('feign', 'httpremoting.shaded.feign')
    relocate('zipkin', 'httpremoting.shaded.zipkin')
    relocate('okhttp3', 'httpremoting.shaded.okhttp3')
    relocate('io.zipkin.brave', 'httpremoting.shaded.io.zipkin.brave')
    relocate('org.joda.time', 'httpremoting.shaded.org.joda.time')
    relocate('okio', 'httpremoting.shaded.okio')
    relocate('com.github.kristofa.brave', 'httpremoting.shaded.com.github.kristofa.brave')
    dependencies {
        exclude(project(':error-handling'))
        exclude(project(':service-config'))
        exclude(project(':ssl-config'))
        exclude(dependency { it.moduleGroup.contains("javax.ws.rs") })
        exclude(dependency { it.moduleGroup.contains("com.fasterxml.jackson") })
    }
    classifier = ''
}

tasks.shadowJar {
    from('licenses/') {
        into 'META-INF/LICENSES'
    }
}

task checkLicenseInformation(dependsOn: 'shadowJar') << {
    def jar = zipTree(tasks.shadowJar.archivePath)
    def licenseFiles = jar.matching { include 'META-INF/LICENSES/*' }
    if (licenseFiles.files.size() != 5) {
        throw new GradleException("Expected 5 META-INF/LICENSES/* files in shadowJar, found: " + licenseFiles.files)
    }
}
check.dependsOn checkLicenseInformation

// Publishing, taken mostly from gradle/publish.gradle.
// This thing is super-brittle, be very careful when making changes.
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'
apply plugin: 'nebula.maven-resolved-dependencies'

publishing.publications {
    shadow(MavenPublication) {
        from components.shadow
    }
}

bintray {
    user = System.env.BINTRAY_USER
    key = System.env.BINTRAY_KEY
    publish = true
    pkg {
        repo = 'releases'
        name = 'http-remoting'
        userOrg = 'palantir'
        licenses = ['Apache-2.0']
        publications = ['shadow']
    }
}

bintrayUpload.onlyIf {
    project.version ==~ /\d+\.\d+\.\d+(-rc[0-9]+)?/
}

bintrayUpload.dependsOn { generatePomFileForShadowPublication }
bintrayUpload.dependsOn { sourceJar }
bintrayUpload.dependsOn { build }
